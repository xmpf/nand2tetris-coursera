// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    
    // X => zx
    // Multiplexer with sel[0]=zx, a=x, b=false
    Mux16(a=x, b=false, sel[0]=zx, out=xmux);

    // X => nx
    // Multiplexer with sel[0]=nx, a=x, b=false
    Not16(in=xmux, out=notxmux);
    Mux16(a=xmux, b=notxmux, sel[0]=nx, out=xmux2);

    // Y => zy
    // Multiplexer with sel[0]=zy, a=x, b=false
    Mux16(a=y, b=false, sel[0]=zy, out=ymux);

    // Y => ny
    // Multiplexer with sel[0]=nx, a=x, b=false
    Not16(in=ymux, out=notymux);
    Mux16(a=ymux, b=notymux, sel[0]=ny, out=ymux2);

    // f, ng
    Add16(a=xmux2, b=ymux2, out=xmuxADDymux);
    And16(a=xmux2, b=ymux2, out=xmuxANDymux);
    Mux16(a=xmuxANDymux, b=xmuxADDymux, sel=f, out=fmux);
    Not16(in=fmux, out=notfmux);
    Mux16(a=fmux, b=notfmux, sel=no, out=out, out[0..7]=lsb, out[8..15]=msb, out[15]=ng);
    
    // zr
    Or8Way(in=lsb, out=iszero0);
    Or8Way(in=msb, out=iszero1);
    Or(a=iszero0, b=iszero1, out=iszero);
    Not(in=iszero, out=zr);
}